#@TYPE: Machine
#@NAME: DuoVero
#@DESCRIPTION: Machine configuration for the Gumstix DuoVero

SOC_FAMILY = "omap4"
TARGET_ARCH = "arm"
# We target cortexa8hf-neon rather than cortexa9hf-neon as the only difference
# is -mtune=cortexa9 and this allows us to be consistent with our Cortex-A8
# builds for Pepper and Overo.
DEFAULTTUNE ?= "cortexa8hf-neon"

require conf/machine/include/soc-family.inc
require conf/machine/include/tune-cortexa8.inc

# Specify kernel recipe
PREFERRED_PROVIDER_virtual/kernel ?= "linux-gumstix"
# Increase this everytime you change something in the kernel
MACHINE_KERNEL_PR = "r0"
KERNEL_DEVICETREE = "omap4-duovero-parlor.dtb"

# Specify u-boot parameters
PREFERRED_PROVIDER_virtual/bootloader = "u-boot"
PREFERRED_PROVIDER_u-boot = "u-boot"

UBOOT_ENTRYPOINT = "0x80008000"
UBOOT_LOADADDRESS = "0x80008000"
UBOOT_MACHINE = "duovero_config"

EXTRA_IMAGEDEPENDS += "u-boot"

# Specify xserver components for builds including X11
PREFERRED_PROVIDER_virtual/xserver = "xserver-xorg"
XSERVER = "xserver-xorg \
           xf86-input-evdev \
           xf86-input-keyboard \
           xf86-input-mouse \
           xf86-video-omapfb"

SERIAL_CONSOLE = "115200 ttyO2"

# tar.b2 for SD card
IMAGE_FSTYPES ?= "tar.bz2"

# Include wifi firmware
MACHINE_EXTRA_RDEPENDS += "linux-firmware-sd8787 linux-firmware-wl18xx"

# Define wifi interface
GUMSTIX_WIFI_IFACE = "mlan0"

# Ship all kernel modules by default
MACHINE_EXTRA_RRECOMMENDS = " kernel-modules kernel-devicetree"
MACHINE_FEATURES = "alsa bluetooth ext2 screen touchscreen usbgadget usbhost vfat wifi"
